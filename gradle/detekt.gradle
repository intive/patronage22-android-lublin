/**
 * Detekt is static code analysis plugin for Kotlin.
 * https://github.com/arturbosch/detekt
 *
 * It can be applied to each project module by adding
 * {@code apply from: rootProject.file("gradle/detekt.gradle")}
 * and creating detekt.yml file in project directory
 */
buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.19.0"
    }

}
apply plugin: "io.gitlab.arturbosch.detekt"

dependencies {
    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:1.19.0"
}

// required to work correctly on Windows
def escapedRootProjectDir = rootProject.projectDir.toString().replace("\\", "/")
def escapedProjectDir = projectDir.toString().replace("\\", "/")

detekt {
    input = files(escapedProjectDir)
    baseline = file("$escapedProjectDir/detekt-baseline.xml")
    config = files("$escapedRootProjectDir/detekt.yml")
    // Users of Gradleâ€™s --parallel flag are encouraged to turn off the parallelism of detekt
    // see https://arturbosch.github.io/detekt/changelog.html#120
    parallel = false
}

// in theory the block should be
// "tasks.withType(io.gitlab.arturbosch.detekt.Detekt)"
// but for some reason all tasks at this point have suffix "_Decorated" so we need to take that into
// consideration
tasks.matching {
    (it instanceof io.gitlab.arturbosch.detekt.Detekt) ||
            it.class.name == "io.gitlab.arturbosch.detekt.Detekt_Decorated"
}.all {
    exclude "**/build/**"
}